apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.5"
}

group = 'karkinos'
version = '2.0.5-SNAPSHOT'

description = """"""

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
     maven { url "https://repo.maven.apache.org/maven2" }
}

sourceSets {
    utils {
        compileClasspath += sourceSets.main.output
        java {
            srcDirs = ['src/utils/java']
        }
        resources {
            srcDirs = ['src/utils/resources']
        }
    }
    test {
        java {
            srcDir 'test/main/java'
        }
    }
}

configurations {
    utilsCompile {
        extendsFrom compile
    }
    testImplementation.extendsFrom compileOnly
}

dependencies {
    compile group: 'de.sciss', name: 'jwave', version:'1.0.3'
    compile group: 'com.github.samtools', name: 'htsjdk', version:'1.129'
    compile group: 'com.github.broadinstitute', name: 'picard', version:'2.0.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version:'3.4'
    compile group: 'com.lowagie', name: 'itext', version:'2.1.5'
    compile group: 'com.soulwarelabs.jcommons', name: 'jcommons-api', version:'1.0.0'
    compile group: 'org.apache.commons', name: 'commons-math', version:'2.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version:'2.0'
    compile group: 'commons-cli', name: 'commons-cli', version:'1.3'
    compile group: 'net.sf.opencsv', name: 'opencsv', version:'2.0'
    compile group: 'com.h2database', name: 'h2', version:'1.4.193'
    compile group: 'commons-logging', name: 'commons-logging', version:'1.2'
    compile group: 'org.apache.poi', name: 'poi', version:'3.8'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version:'3.14'
    compile group: 'org.apache.poi', name: 'ooxml-schemas', version:'1.3'
    compile group: 'org.apache.poi', name: 'poi-contrib', version:'3.6'
    compile group: 'dom4j', name: 'dom4j', version:'1.6.1'
    compile group: 'stax', name: 'stax-api', version:'1.0.1'
    compile group: 'org.jfree', name: 'jfreechart', version:'1.0.19'
    compile group: 'org.apache.commons', name: 'commons-math3', version:'3.6'
    compile fileTree(dir: "dependencies", include: ["*.jar"])
    utilsCompile sourceSets.test.output
    testCompile group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.3.1'
    testCompile group: 'org.junit.platform', name: 'junit-platform-console', version: '1.3.1'
    testCompile group: 'org.junit.platform', name: 'junit-platform-console-standalone', version: '1.3.1'
    testCompile group: 'org.junit.platform', name: 'junit-platform-engine', version: '1.3.1'
    testCompile group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.3.1'
    testCompile group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.3.1'
    testCompile group: 'org.junit.platform', name: 'junit-platform-suite-api', version: '1.3.1'
    testCompile group: 'org.junit.platform', name: 'junit-platform-surefire-provider', version: '1.3.1'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.3.1'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.1'
    compileOnly 'systems.manifold:manifold-ext:2020.1.24'
    testImplementation 'systems.manifold:manifold-ext-rt:2020.1.24'
    annotationProcessor group: 'systems.manifold', name: 'manifold-ext', version: '2020.1.24'
}

def commitHash = { ->
    def baos = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = baos
    }
    return baos.toString().trim()
}

task uberjar(type: Jar) {
  baseName = project.name + '-standalone'
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
  manifest {
    attributes 'Implementation-Title': project.name
    attributes 'Implementation-Version': project.version + '_' + commitHash()
    attributes 'Main-Class': 'jp.ac.utokyo.rcast.karkinos.cmd.KarkinosCmd'
  }
  exclude "META-INF/*.RSA"
  exclude "META-INF/*.DSA"
  exclude "META-INF/*.SF"
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
    }
}

task utilsJar(type: Jar) {
    from sourceSets.utils.output
}

test {
    test.finalizedBy jacocoTestReport
    useJUnitPlatform()
}

if (JavaVersion.current() != JavaVersion.VERSION_1_8 &&
    sourceSets.main.allJava.files.any {it.name == "module-info.java"}) {
    compileTestJava {
        // if you DO define a module-info.java file:
        options.compilerArgs += ['-Xplugin:Manifold', '--module-path', it.classpath.asPath]
    }
} else {
    compileTestJava {
        // If you DO NOT define a module-info.java file:
        options.compilerArgs += ['-Xplugin:Manifold']
    }
}
